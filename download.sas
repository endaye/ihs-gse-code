** SAS Code Source: **;
** http://goo.gl/svINXV **;
** 02/04/2015 modified by York **;

OPTIONS COMPRESS=yes;
OPTIONS MPRINT;
LIBNAME f "../sas_dataset/fannie/";
%LET AQSN_HEADER =
	LOAN_ID :$20.
	CHANNEL :$1.
	SELLER :$80.
	ORIG_RATE
	ORIG_AMT
	ORIG_TERM
	X_ORIG_DATE :$7.
	X_FIRST_PAY :$7.
	OLTV
	OCLTV
	NUMBER_BORROWERS
	DTI
	BORROWER_FICO
	FIRST_TIME_HB :$1.
	LOAN_PURPOSE :$1.
	PROP_TYPE :$2.
	NUMBER_UNITS
	OCCUPANCY :$1.
	STATE :$2.
	ZIP_3 :$5.
	MI_COVERAGE_PCT
	PRODUCT_TYPE :$3.
	COBORROWER_FICO
	;

%LET AQSN_LABEL = 
	LOAN_ID	= "LOAN IDENTIFIER"
	CHANNEL = "CHANNEL"
	SELLER	= "SELLER NAME"
	ORIG_RATE	= "ORIGINAL INTEREST RATE"
	ORIG_AMT	= "ORIGINAL UNPAID PRINCIPAL BALANCE (UPB)"
	ORIG_TERM	= "ORIGINAL LOAN TERM"
	X_ORIG_DATE	= "ORIGINATION DATE"
	X_FIRST_PAY = "FIRST PAYMENT DATE"
	OLTV	= "ORIGINAL LOAN-TO-VALUE (LTV)"
	OCLTV	= "ORIGINAL COMBINED LOAN-TO-VALUE (CLTV)"
	NUMBER_BORROWERS	= "NUMBER OF BORROWERS"
	DTI	= "DEBT-TO-INCOME RATIO"
	BORROWER_FICO	= "BORROWER CREDIT SCORE"
	FIRST_TIME_HB 	= "FIRST-TIME HOME BUYER INDICATOR"
	LOAN_PURPOSE	= "LOAN PURPOSE"
	PROP_TYPE	= "PROPERTY TYPE"
	NUMBER_UNITS	= "NUMBER OF UNITS"
	OCCUPANCY	= "OCCUPANCY STATUS"
	STATE	= "PROPERTY STATE"
	ZIP_3	= "ZIP (3-DIGIT)"
	MI_COVERAGE_PCT	= "MORTGAGE INSURANCE PERCENTAGE"
	PRODUCT_TYPE	= "PRODUCT TYPE"
	COBORROWER_FICO = "CO BORROWER CREDIT SCORE"
	;

%LET PERF_HEADER =
	LOAN_ID :$20.
	X_ACTIVITY_PERIOD :$10.
	SERVICER :$80.
	CURRENT_RATE
	CURRENT_UPB
	LOAN_AGE
	REMAINING_MONTHS
	ADJ_REMAINING_MONTHS
	X_MATURITY_DATE :$7.
	MSA :$5.
	CURRENT_DLQ_STATUS :$5.
	MOD_FLAG :$1.
	ZERO_BALANCE_CODE :$2.
	X_ZERO_BALANCE_DATE :$7.
	X_REPURCHASE_DATE :$10.
	;

%LET PERF_LABEL = 
	LOAN_ID	= "LOAN IDENTIFIER"
	X_ACTIVITY_PERIOD	= "MONTHLY REPORTING PERIOD"
	SERVICER	= "SERVICER NAME"
	CURRENT_RATE	= "CURRENT INTEREST RATE"
	CURRENT_UPB	= "CURRENT ACTUAL UNPAID PRINCIPAL BALANCE (UPB)"
	LOAN_AGE	= "LOAN AGE"
	REMAINING_MONTHS	= "REMAINING MONTHS TO LEGAL MATURITY"
	ADJ_REMAINING_MONTHS	= "ADJUSTED REMAINING MONTHS TO MATURITY"
	X_MATURITY_DATE	= "MATURITY DATE"
	MSA	= "METROPOLITAN STATISTICAL AREA"
	CURRENT_DLQ_STATUS	= "CURRENT LOAN DELINQUENCY STATUS"
	MOD_FLAG	= "MODIFICATION FLAG"
	ZERO_BALANCE_CODE	= "ZERO BALANCE CODE"
	X_ZERO_BALANCE_DATE	= "ZERO BALANCE EFFECTIVE DATE"
	X_REPURCHASE_DATE	= "REPURCHASE DATE"
	;

%MACRO FILE_IMPORT (QUARTER);
	data f.AQSN_&QUARTER (DROP=X_ORIG_DATE X_FIRST_PAY);
		infile "../data/fannie/Acquisition_&quarter..txt" delimiter = '|' MISSOVER DSD lrecl=32767;
		input &AQSN_HEADER;
		label &AQSN_LABEL;
		format ORIG_DATE mmddyy8. FIRST_PAY_DATE mmddyy8.;
		** Origination Date - Convert MM/YYYY string to SAS Date **;
		ORIG_DATE = mdy(input(substr(X_ORIG_DATE,1,2),2.),1,input(substr(X_ORIG_DATE,4,4),4.));
		** First Payment Date - Convert MM/YYYY string to SAS Date **;
		FIRST_PAY_DATE = mdy(input(substr(X_FIRST_PAY,1,2),2.),1,input(substr(X_FIRST_PAY,4,4),4.));
	run;

	data f.PERF_&QUARTER (DROP=X_ACTIVITY_PERIOD X_MATURITY_DATE X_ZERO_BALANCE_DATE X_REPURCHASE_DATE);
		infile "../data/fannie/Performance_&quarter..txt" delimiter = '|' MISSOVER DSD lrecl=32767;
		input &PERF_HEADER;
		label &PERF_LABEL;
		format ACTIVITY_PERIOD mmddyy8. MATURITY_DATE mmddyy8. ZERO_BALANCE_DATE mmddyy8.
		REPURCH_PERIOD mmddyy8.;
		** Activity Period - Convert MM/DD/YYYY string to SAS Date **;
		ACTIVITY_PERIOD = mdy(input(substr(X_ACTIVITY_PERIOD,1,2),2.),1,input(substr(X_ACTIVITY_PERIOD,7,4),4.));
		** Maturity Date - Convert MM/YYYY string to SAS Date **;
		MATURITY_DATE = mdy(input(substr(X_MATURITY_DATE,1,2),2.),1,input(substr(X_MATURITY_DATE,4,4),4.));
		** Zero Balance Date - Convert MM/YYYY string to SAS Date **;
		ZERO_BALANCE_DATE = mdy(input(substr(X_ZERO_BALANCE_DATE,1,2),2.),1,input(substr(X_ZERO_BALANCE_DATE,4,4),4.));
		** Repurchase Date - Convert MM/DD/YYYY string to SAS Date **;
		REPURCH_PERIOD = mdy(input(substr(X_REPURCHASE_DATE,1,2),2.),1,input(substr(X_REPURCHASE_DATE,7,4),4.));
	run;

%MEND;

*%FILE_IMPORT(2000Q1);
